(* Content-type: application/vnd.wolfram.mathematica *)

(*** Wolfram Notebook File ***)
(* http://www.wolfram.com/nb *)

(* CreatedBy='Mathematica 10.0' *)

(*CacheID: 234*)
(* Internal cache information:
NotebookFileLineBreakTest
NotebookFileLineBreakTest
NotebookDataPosition[       158,          7]
NotebookDataLength[     38183,       1323]
NotebookOptionsPosition[     33598,       1173]
NotebookOutlinePosition[     34385,       1199]
CellTagsIndexPosition[     34342,       1196]
WindowFrame->Normal*)

(* Beginning of Notebook Content *)
Notebook[{

Cell[CellGroupData[{
Cell["Curvature and the Einstein Equation", "Subtitle"],

Cell[TextData[{
 "This is the ",
 StyleBox["Mathematica",
  FontSlant->"Italic"],
 " notebook ",
 StyleBox["Curvature and the Einstein Equation ",
  FontSlant->"Italic"],
 StyleBox["available from the book website.  F",
  FontVariations->{"CompatibilityType"->0}],
 "rom a given metric ",
 Cell[BoxData[
  FormBox[
   SubscriptBox[
    StyleBox["g",
     FontSlant->"Italic"], "\[Alpha]\[Beta]"], TraditionalForm]]],
 " , it computes the components of the following: the inverse metric, ",
 Cell[BoxData[
  FormBox[
   SuperscriptBox["g", "\[Lambda]\[Sigma]"], TraditionalForm]]],
 ", the Christoffel symbols or affine connection,"
}], "Text"],

Cell[TextData[{
 Cell[BoxData[
  FormBox[
   RowBox[{
    SubscriptBox[
     SuperscriptBox["\[CapitalGamma]", "\[Lambda]"], "\[Mu]\[Nu]"], "=", 
    RowBox[{
     FractionBox["1", "2"], 
     RowBox[{
      SuperscriptBox["g", "\[Lambda]\[Sigma]"], "(", 
      RowBox[{
       RowBox[{
        SubscriptBox["\[PartialD]", "\[Mu]"], " ", 
        SubscriptBox["g", "\[Sigma]\[Nu]"]}], "+", 
       RowBox[{
        SubscriptBox["\[PartialD]", "\[Nu]"], " ", 
        SubscriptBox["g", "\[Sigma]\[Mu]"]}], "-", 
       RowBox[{
        SubscriptBox["\[PartialD]", "\[Sigma]"], " ", 
        SubscriptBox["g", "\[Mu]\[Nu]"]}]}], ")"}]}]}], TraditionalForm]]],
 ","
}], "Text"],

Cell[TextData[{
 " ( ",
 Cell[BoxData[
  FormBox[
   SubscriptBox["\[PartialD]", 
    RowBox[{"\[Alpha]", "  "}]], TraditionalForm]]],
 "stands for the partial derivative ",
 Cell[BoxData[
  FormBox[
   RowBox[{"\[PartialD]", 
    RowBox[{"/", 
     RowBox[{"\[PartialD]", 
      SuperscriptBox["x", "\[Alpha]"]}]}]}], TraditionalForm]]],
 "), the Riemann tensor,"
}], "Text"],

Cell[TextData[{
 Cell[BoxData[
  FormBox[
   RowBox[{
    SubscriptBox[
     SuperscriptBox["R", "\[Lambda]"], "\[Mu]\[Nu]\[Sigma]"], "=", 
    RowBox[{
     RowBox[{
      SubscriptBox["\[PartialD]", "\[Nu]"], " ", 
      SubscriptBox[
       SuperscriptBox["\[CapitalGamma]", "\[Lambda]"], "\[Mu]\[Sigma]"]}], 
     "-", 
     RowBox[{
      SubscriptBox["\[PartialD]", "\[Sigma]"], " ", 
      SubscriptBox[
       SuperscriptBox["\[CapitalGamma]", "\[Lambda]"], "\[Mu]\[Nu]"]}], "+", 
     RowBox[{
      SubscriptBox[
       SuperscriptBox["\[CapitalGamma]", "\[Eta]"], "\[Mu]\[Sigma]"], 
      SubscriptBox[
       SuperscriptBox["\[CapitalGamma]", "\[Lambda]"], "\[Eta]\[Nu]"]}], "-", 
     
     RowBox[{
      SubscriptBox[
       SuperscriptBox["\[CapitalGamma]", "\[Eta]"], "\[Mu]\[Nu]"], 
      SubscriptBox[
       SuperscriptBox["\[CapitalGamma]", "\[Lambda]"], 
       "\[Eta]\[Sigma]"]}]}]}], TraditionalForm]]],
 ","
}], "Text"],

Cell["the Ricci tensor", "Text"],

Cell[TextData[{
 Cell[BoxData[
  FormBox[
   RowBox[{
    SubscriptBox["R", "\[Mu]\[Nu]"], "=", 
    SubscriptBox[
     SuperscriptBox["R", "\[Lambda]"], "\[Mu]\[Lambda]\[Nu]"]}], 
   TraditionalForm]]],
 ","
}], "Text"],

Cell["the scalar curvature,", "Text"],

Cell[TextData[{
 Cell[BoxData[
  FormBox[
   RowBox[{"R", "=", 
    RowBox[{
     SuperscriptBox["g", "\[Mu]\[Nu]"], 
     SubscriptBox["R", "\[Mu]\[Nu]"]}]}], TraditionalForm]]],
 ","
}], "Text"],

Cell["and the Einstein tensor,", "Text"],

Cell[TextData[{
 " ",
 Cell[BoxData[
  FormBox[
   RowBox[{
    SubscriptBox["G", "\[Mu]\[Nu]"], "=", 
    RowBox[{
     SubscriptBox["R", "\[Mu]\[Nu]"], "-", 
     RowBox[{
      FractionBox["1", "2"], 
      SubscriptBox["g", "\[Mu]\[Nu]"], "R"}]}]}], TraditionalForm]]],
 "."
}], "Text"],

Cell[TextData[{
 "You must input the covariant components of the metric tensor ",
 Cell[BoxData[
  FormBox[
   SubscriptBox["g", "\[Mu]\[Nu]"], TraditionalForm]]],
 " by editing the relevant input line in this ",
 StyleBox["Mathematica",
  FontSlant->"Italic"],
 " notebook. You may also wish to change the names of the coordinates. Only \
the nonzero components of the above quantities are displayed as the output. \
All the components computed are in the ",
 StyleBox["coordinate basis ",
  FontSlant->"Italic"],
 StyleBox["in which the metric was specified. ",
  FontVariations->{"CompatibilityType"->0}]
}], "Text"],

Cell[CellGroupData[{

Cell["Clearing the values of symbols:", "Subsubsection"],

Cell["\<\
First clear any values that may already have been assigned to the names of \
the various objects to be calculated. The names of the coordinates that you \
will use are also cleared.\
\>", "Text"],

Cell[BoxData[
 RowBox[{"Clear", "[", 
  RowBox[{
  "coord", ",", " ", "metric", ",", "inversemetric", ",", " ", "affine", ",", 
   " ", "riemann", ",", " ", "ricci", ",", " ", "scalar", ",", " ", 
   "einstein", ",", "r", ",", "\[Theta]", ",", "\[Phi]", ",", "t"}], 
  "]"}]], "Input"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Setting the dimension:", "Subsubsection"],

Cell[TextData[{
 "The dimension ",
 StyleBox["n ",
  FontWeight->"Bold"],
 StyleBox["of the spacetime (or space) must be set:",
  FontVariations->{"CompatibilityType"->0}]
}], "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"n", "=", "4"}]], "Input"],

Cell[BoxData["4"], "Output",
 CellChangeTimes->{3.646566250098402*^9}]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Defining a list of coordinates:", "Subsubsection"],

Cell["\<\
The example given here is the Schwarzschild metric. The coordinate choice of \
Schwarzschild is appropriate for this spherically symmetric spacetime. \
\>", "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 StyleBox[
  RowBox[{"coord", " ", "=", " ", 
   RowBox[{"{", 
    RowBox[{"r", ",", "\[Theta]", ",", "\[Phi]", ",", "t"}], "}"}]}],
  FontWeight->"Bold"]], "Input"],

Cell[BoxData[
 RowBox[{"{", 
  RowBox[{"r", ",", "\[Theta]", ",", "\[Phi]", ",", "t"}], "}"}]], "Output",
 CellChangeTimes->{3.646566250168985*^9}]
}, Open  ]],

Cell[TextData[{
 "You can change the names of the coordinates by simply editing the \
definition of ",
 StyleBox["coord",
  FontWeight->"Bold"],
 ", for example, to ",
 StyleBox["coord = {x, y, z, t}",
  FontWeight->"Bold"],
 ", when another set of coordinate names is more appropriate. In this program \
indices range over ",
 StyleBox["1 ",
  FontWeight->"Bold"],
 StyleBox["to ",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox["n.  ",
  FontWeight->"Bold",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox["Thus for spacetime they range from 1 to 4 and ",
  FontVariations->{"CompatibilityType"->0}],
 Cell[BoxData[
  FormBox[
   SuperscriptBox["x", "4"], TraditionalForm]]],
 " is the same as ",
 Cell[BoxData[
  FormBox[
   SuperscriptBox["x", "0"], TraditionalForm]]],
 " used in the text. "
}], "Text"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Defining the metric:", "Subsubsection"],

Cell["\<\
Input the metric as a list of lists, i.e., as a matrix. You can input the \
components of any metric here, but you must specify them as explicit \
functions of the coordinates.\
\>", "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"metric", "=", 
  RowBox[{"{", 
   RowBox[{
    RowBox[{"{", 
     RowBox[{
      RowBox[{
       RowBox[{"(", 
        RowBox[{"1", "-", 
         RowBox[{"2", " ", 
          RowBox[{"m", "/", "r"}]}]}], ")"}], "^", 
       RowBox[{"(", 
        RowBox[{"-", "1"}], ")"}]}], ",", "0", ",", "0", ",", "0"}], "}"}], 
    ",", 
    RowBox[{"{", 
     RowBox[{"0", ",", 
      RowBox[{"r", "^", "2"}], ",", "0", ",", "0"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"0", ",", "0", ",", 
      RowBox[{
       RowBox[{"r", "^", "2"}], " ", 
       RowBox[{
        RowBox[{"Sin", "[", "\[Theta]", "]"}], "^", "2"}]}], ",", "0"}], 
     "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"0", ",", "0", ",", "0", ",", 
      RowBox[{"-", 
       RowBox[{"(", 
        RowBox[{"1", "-", 
         RowBox[{"2", " ", 
          RowBox[{"m", "/", "r"}]}]}], ")"}]}]}], "}"}]}], "}"}]}]], "Input"],

Cell[BoxData[
 RowBox[{"{", 
  RowBox[{
   RowBox[{"{", 
    RowBox[{
     FractionBox["1", 
      RowBox[{"1", "-", 
       FractionBox[
        RowBox[{"2", " ", "m"}], "r"]}]], ",", "0", ",", "0", ",", "0"}], 
    "}"}], ",", 
   RowBox[{"{", 
    RowBox[{"0", ",", 
     SuperscriptBox["r", "2"], ",", "0", ",", "0"}], "}"}], ",", 
   RowBox[{"{", 
    RowBox[{"0", ",", "0", ",", 
     RowBox[{
      SuperscriptBox["r", "2"], " ", 
      SuperscriptBox[
       RowBox[{"Sin", "[", "\[Theta]", "]"}], "2"]}], ",", "0"}], "}"}], ",", 
   
   RowBox[{"{", 
    RowBox[{"0", ",", "0", ",", "0", ",", 
     RowBox[{
      RowBox[{"-", "1"}], "+", 
      FractionBox[
       RowBox[{"2", " ", "m"}], "r"]}]}], "}"}]}], "}"}]], "Output",
 CellChangeTimes->{3.6465662502020903`*^9}]
}, Open  ]],

Cell["You can also display this in matrix form.", "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"metric", "//", "MatrixForm"}]], "Input"],

Cell[BoxData[
 TagBox[
  RowBox[{"(", "\[NoBreak]", GridBox[{
     {
      FractionBox["1", 
       RowBox[{"1", "-", 
        FractionBox[
         RowBox[{"2", " ", "m"}], "r"]}]], "0", "0", "0"},
     {"0", 
      SuperscriptBox["r", "2"], "0", "0"},
     {"0", "0", 
      RowBox[{
       SuperscriptBox["r", "2"], " ", 
       SuperscriptBox[
        RowBox[{"Sin", "[", "\[Theta]", "]"}], "2"]}], "0"},
     {"0", "0", "0", 
      RowBox[{
       RowBox[{"-", "1"}], "+", 
       FractionBox[
        RowBox[{"2", " ", "m"}], "r"]}]}
    },
    GridBoxAlignment->{
     "Columns" -> {{Center}}, "ColumnsIndexed" -> {}, "Rows" -> {{Baseline}}, 
      "RowsIndexed" -> {}},
    GridBoxSpacings->{"Columns" -> {
        Offset[0.27999999999999997`], {
         Offset[0.7]}, 
        Offset[0.27999999999999997`]}, "ColumnsIndexed" -> {}, "Rows" -> {
        Offset[0.2], {
         Offset[0.4]}, 
        Offset[0.2]}, "RowsIndexed" -> {}}], "\[NoBreak]", ")"}],
  Function[BoxForm`e$, 
   MatrixForm[BoxForm`e$]]]], "Output",
 CellChangeTimes->{3.646566250222789*^9}]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Note:", "Subsubsection"],

Cell[TextData[{
 "It is important not to use the symbols, ",
 StyleBox["i",
  FontWeight->"Bold"],
 ", ",
 StyleBox["j",
  FontWeight->"Bold"],
 ", ",
 StyleBox["k",
  FontWeight->"Bold"],
 ", ",
 StyleBox["l,",
  FontWeight->"Bold"],
 StyleBox[" ",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox["s, ",
  FontWeight->"Bold",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox["or ",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox["n",
  FontWeight->"Bold",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox[" ",
  FontWeight->"Bold"],
 "as constants or coordinates in the metric that you specify above. The \
reason is that the first five of  those symbols are used as summation or \
table indices in the calculations done below, and ",
 StyleBox["n ",
  FontWeight->"Bold"],
 StyleBox["is the dimension of the space. ",
  FontVariations->{"CompatibilityType"->0}],
 "For example, if ",
 StyleBox["m",
  FontWeight->"Bold"],
 " were used as a summation or table index below, then you would get the \
wrong answer for the present metric because the ",
 StyleBox["m",
  FontWeight->"Bold"],
 " in the metric would be treated as an index, rather than as the mass."
}], "Text"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Calculating the inverse metric:", "Subsubsection"],

Cell["The inverse metric is obtained through matrix inversion.", "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"inversemetric", "=", 
  RowBox[{"Simplify", "[", 
   RowBox[{"Inverse", "[", "metric", "]"}], "]"}]}]], "Input"],

Cell[BoxData[
 RowBox[{"{", 
  RowBox[{
   RowBox[{"{", 
    RowBox[{
     RowBox[{"1", "-", 
      FractionBox[
       RowBox[{"2", " ", "m"}], "r"]}], ",", "0", ",", "0", ",", "0"}], "}"}],
    ",", 
   RowBox[{"{", 
    RowBox[{"0", ",", 
     FractionBox["1", 
      SuperscriptBox["r", "2"]], ",", "0", ",", "0"}], "}"}], ",", 
   RowBox[{"{", 
    RowBox[{"0", ",", "0", ",", 
     FractionBox[
      SuperscriptBox[
       RowBox[{"Csc", "[", "\[Theta]", "]"}], "2"], 
      SuperscriptBox["r", "2"]], ",", "0"}], "}"}], ",", 
   RowBox[{"{", 
    RowBox[{"0", ",", "0", ",", "0", ",", 
     FractionBox["r", 
      RowBox[{
       RowBox[{"2", " ", "m"}], "-", "r"}]]}], "}"}]}], "}"}]], "Output",
 CellChangeTimes->{3.646566250256112*^9}]
}, Open  ]],

Cell["This can also be displayed in matrix form:", "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"inversemetric", "//", "MatrixForm"}]], "Input"],

Cell[BoxData[
 TagBox[
  RowBox[{"(", "\[NoBreak]", GridBox[{
     {
      RowBox[{"1", "-", 
       FractionBox[
        RowBox[{"2", " ", "m"}], "r"]}], "0", "0", "0"},
     {"0", 
      FractionBox["1", 
       SuperscriptBox["r", "2"]], "0", "0"},
     {"0", "0", 
      FractionBox[
       SuperscriptBox[
        RowBox[{"Csc", "[", "\[Theta]", "]"}], "2"], 
       SuperscriptBox["r", "2"]], "0"},
     {"0", "0", "0", 
      FractionBox["r", 
       RowBox[{
        RowBox[{"2", " ", "m"}], "-", "r"}]]}
    },
    GridBoxAlignment->{
     "Columns" -> {{Center}}, "ColumnsIndexed" -> {}, "Rows" -> {{Baseline}}, 
      "RowsIndexed" -> {}},
    GridBoxSpacings->{"Columns" -> {
        Offset[0.27999999999999997`], {
         Offset[0.7]}, 
        Offset[0.27999999999999997`]}, "ColumnsIndexed" -> {}, "Rows" -> {
        Offset[0.2], {
         Offset[0.4]}, 
        Offset[0.2]}, "RowsIndexed" -> {}}], "\[NoBreak]", ")"}],
  Function[BoxForm`e$, 
   MatrixForm[BoxForm`e$]]]], "Output",
 CellChangeTimes->{3.646566250286315*^9}]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Calculating the Christoffel symbols:", "Subsubsection"],

Cell[TextData[{
 "The calculation of the components of the Christoffel symbols is done by \
transcribing the definition given earlier into the notation of ",
 StyleBox["Mathematica",
  FontSlant->"Italic"],
 " and using the ",
 StyleBox["Mathematica",
  FontSlant->"Italic"],
 " functions ",
 StyleBox["D",
  FontWeight->"Bold"],
 " for taking partial derivatives, ",
 StyleBox["Sum",
  FontWeight->"Bold"],
 " for summing over repeated indices, ",
 StyleBox["Table",
  FontWeight->"Bold"],
 " for forming a list of components, and ",
 StyleBox["Simplify",
  FontWeight->"Bold"],
 " for simplifying the result."
}], "Text"],

Cell[BoxData[
 RowBox[{"affine", ":=", 
  RowBox[{"affine", "=", 
   RowBox[{"Simplify", "[", 
    RowBox[{"Table", "[", 
     RowBox[{
      RowBox[{
       RowBox[{"(", 
        RowBox[{"1", "/", "2"}], ")"}], "*", 
       RowBox[{"Sum", "[", 
        RowBox[{
         RowBox[{
          RowBox[{"(", 
           RowBox[{"inversemetric", "[", 
            RowBox[{"[", 
             RowBox[{"i", ",", "s"}], "]"}], "]"}], ")"}], "*", 
          "\[IndentingNewLine]", 
          RowBox[{"(", 
           RowBox[{
            RowBox[{"D", "[", 
             RowBox[{
              RowBox[{"metric", "[", 
               RowBox[{"[", 
                RowBox[{"s", ",", "j"}], "]"}], "]"}], ",", 
              RowBox[{"coord", "[", 
               RowBox[{"[", "k", "]"}], "]"}]}], " ", "]"}], "+", 
            "\[IndentingNewLine]", 
            RowBox[{"D", "[", 
             RowBox[{
              RowBox[{"metric", "[", 
               RowBox[{"[", 
                RowBox[{"s", ",", "k"}], "]"}], "]"}], ",", 
              RowBox[{"coord", "[", 
               RowBox[{"[", "j", "]"}], "]"}]}], " ", "]"}], "-", 
            RowBox[{"D", "[", 
             RowBox[{
              RowBox[{"metric", "[", 
               RowBox[{"[", 
                RowBox[{"j", ",", "k"}], "]"}], "]"}], ",", 
              RowBox[{"coord", "[", 
               RowBox[{"[", "s", "]"}], "]"}]}], " ", "]"}]}], ")"}]}], ",", 
         RowBox[{"{", 
          RowBox[{"s", ",", "1", ",", "n"}], "}"}]}], "]"}]}], ",", 
      "\[IndentingNewLine]", 
      RowBox[{"{", 
       RowBox[{"i", ",", "1", ",", "n"}], "}"}], ",", 
      RowBox[{"{", 
       RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
      RowBox[{"{", 
       RowBox[{"k", ",", "1", ",", "n"}], "}"}]}], "]"}], " ", 
    "]"}]}]}]], "Input"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Displaying the Christoffel symbols:", "Subsubsection"],

Cell[TextData[{
 "The nonzero Christoffel symbols are displayed below. You need not follow \
the details of constructing the functions that we use for that purpose.  In \
the output the symbol \[CapitalGamma][1,2,3] stands for",
 StyleBox[" ",
  FontSlant->"Italic"],
 Cell[BoxData[
  FormBox[
   SubscriptBox[
    SuperscriptBox["\[CapitalGamma]", "1"], "23"], TraditionalForm]]],
 ". Because the Christoffel symbols are symmetric under interchange of the \
last two indices, only the independent components are displayed."
}], "Text"],

Cell[BoxData[
 RowBox[{"listaffine", ":=", 
  RowBox[{"Table", "[", 
   RowBox[{
    RowBox[{"If", "[", 
     RowBox[{
      RowBox[{"UnsameQ", "[", 
       RowBox[{
        RowBox[{"affine", "[", 
         RowBox[{"[", 
          RowBox[{"i", ",", "j", ",", "k"}], "]"}], "]"}], ",", "0"}], "]"}], 
      ",", 
      RowBox[{"{", 
       RowBox[{
        RowBox[{"ToString", "[", 
         RowBox[{"\[CapitalGamma]", "[", 
          RowBox[{"i", ",", "j", ",", "k"}], "]"}], "]"}], ",", 
        RowBox[{"affine", "[", 
         RowBox[{"[", 
          RowBox[{"i", ",", "j", ",", "k"}], "]"}], "]"}]}], "}"}]}], "]"}], 
    " ", ",", 
    RowBox[{"{", 
     RowBox[{"i", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"k", ",", "1", ",", "j"}], "}"}]}], "]"}]}]], "Input"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"TableForm", "[", 
  RowBox[{
   RowBox[{"Partition", "[", 
    RowBox[{
     RowBox[{"DeleteCases", "[", 
      RowBox[{
       RowBox[{"Flatten", "[", "listaffine", "]"}], ",", "Null"}], "]"}], ",",
      "2"}], "]"}], ",", 
   RowBox[{"TableSpacing", "\[Rule]", 
    RowBox[{"{", 
     RowBox[{"2", ",", "2"}], "}"}]}]}], "]"}]], "Input"],

Cell[BoxData[
 TagBox[GridBox[{
    {"\<\"\[CapitalGamma][1, 1, 1]\"\>", 
     FractionBox["m", 
      RowBox[{
       RowBox[{"2", " ", "m", " ", "r"}], "-", 
       SuperscriptBox["r", "2"]}]]},
    {"\<\"\[CapitalGamma][1, 2, 2]\"\>", 
     RowBox[{
      RowBox[{"2", " ", "m"}], "-", "r"}]},
    {"\<\"\[CapitalGamma][1, 3, 3]\"\>", 
     RowBox[{
      RowBox[{"(", 
       RowBox[{
        RowBox[{"2", " ", "m"}], "-", "r"}], ")"}], " ", 
      SuperscriptBox[
       RowBox[{"Sin", "[", "\[Theta]", "]"}], "2"]}]},
    {"\<\"\[CapitalGamma][1, 4, 4]\"\>", 
     FractionBox[
      RowBox[{"m", " ", 
       RowBox[{"(", 
        RowBox[{
         RowBox[{
          RowBox[{"-", "2"}], " ", "m"}], "+", "r"}], ")"}]}], 
      SuperscriptBox["r", "3"]]},
    {"\<\"\[CapitalGamma][2, 2, 1]\"\>", 
     FractionBox["1", "r"]},
    {"\<\"\[CapitalGamma][2, 3, 3]\"\>", 
     RowBox[{
      RowBox[{"-", 
       RowBox[{"Cos", "[", "\[Theta]", "]"}]}], " ", 
      RowBox[{"Sin", "[", "\[Theta]", "]"}]}]},
    {"\<\"\[CapitalGamma][3, 3, 1]\"\>", 
     FractionBox["1", "r"]},
    {"\<\"\[CapitalGamma][3, 3, 2]\"\>", 
     RowBox[{"Cot", "[", "\[Theta]", "]"}]},
    {"\<\"\[CapitalGamma][4, 4, 1]\"\>", 
     RowBox[{"-", 
      FractionBox["m", 
       RowBox[{
        RowBox[{"2", " ", "m", " ", "r"}], "-", 
        SuperscriptBox["r", "2"]}]]}]}
   },
   GridBoxAlignment->{
    "Columns" -> {{Left}}, "ColumnsIndexed" -> {}, "Rows" -> {{Baseline}}, 
     "RowsIndexed" -> {}},
   GridBoxSpacings->{"Columns" -> {
       Offset[0.27999999999999997`], {
        Offset[1.4]}, 
       Offset[0.27999999999999997`]}, "ColumnsIndexed" -> {}, "Rows" -> {
       Offset[0.2], {
        Offset[0.8]}, 
       Offset[0.2]}, "RowsIndexed" -> {}}],
  Function[BoxForm`e$, 
   TableForm[BoxForm`e$, TableSpacing -> {2, 2}]]]], "Output",
 CellChangeTimes->{3.646566250349766*^9}]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Calculating and displaying the Riemann tensor:", "Subsubsection"],

Cell[TextData[{
 "The components of the Riemann tensor, ",
 Cell[BoxData[
  FormBox[
   SubscriptBox[
    SuperscriptBox["R", "\[Lambda]"], "\[Mu]\[Nu]\[Sigma]"], 
   TraditionalForm]]],
 ", are calculated using the definition given above."
}], "Text"],

Cell[BoxData[
 RowBox[{"riemann", ":=", 
  RowBox[{"riemann", "=", 
   RowBox[{"Simplify", "[", 
    RowBox[{"Table", "[", "\[IndentingNewLine]", 
     RowBox[{
      RowBox[{
       RowBox[{"D", "[", 
        RowBox[{
         RowBox[{"affine", "[", 
          RowBox[{"[", 
           RowBox[{"i", ",", "j", ",", "l"}], "]"}], "]"}], ",", 
         RowBox[{"coord", "[", 
          RowBox[{"[", "k", "]"}], "]"}]}], " ", "]"}], "-", 
       RowBox[{"D", "[", 
        RowBox[{
         RowBox[{"affine", "[", 
          RowBox[{"[", 
           RowBox[{"i", ",", "j", ",", "k"}], "]"}], "]"}], ",", 
         RowBox[{"coord", "[", 
          RowBox[{"[", "l", "]"}], "]"}]}], " ", "]"}], "+", 
       "\[IndentingNewLine]", 
       RowBox[{"Sum", "[", 
        RowBox[{
         RowBox[{
          RowBox[{
           RowBox[{"affine", "[", 
            RowBox[{"[", 
             RowBox[{"s", ",", "j", ",", "l"}], "]"}], "]"}], " ", 
           RowBox[{"affine", "[", 
            RowBox[{"[", 
             RowBox[{"i", ",", "k", ",", "s"}], "]"}], "]"}]}], "-", 
          RowBox[{
           RowBox[{"affine", "[", 
            RowBox[{"[", 
             RowBox[{"s", ",", "j", ",", "k"}], "]"}], "]"}], " ", 
           RowBox[{"affine", "[", 
            RowBox[{"[", 
             RowBox[{"i", ",", "l", ",", "s"}], "]"}], "]"}]}]}], ",", 
         "\[IndentingNewLine]", 
         RowBox[{"{", 
          RowBox[{"s", ",", "1", ",", "n"}], "}"}]}], "]"}]}], ",", 
      "\[IndentingNewLine]", 
      RowBox[{"{", 
       RowBox[{"i", ",", "1", ",", "n"}], "}"}], ",", 
      RowBox[{"{", 
       RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
      RowBox[{"{", 
       RowBox[{"k", ",", "1", ",", "n"}], "}"}], ",", 
      RowBox[{"{", 
       RowBox[{"l", ",", "1", ",", "n"}], "}"}]}], "]"}], " ", 
    "]"}]}]}]], "Input"],

Cell[TextData[{
 "The nonzero components are displayed by the following functions. In the \
output, the symbol R[1, 2, 1, 3] stands for ",
 Cell[BoxData[
  FormBox[
   SubscriptBox[
    SuperscriptBox["R", "1"], "213"], TraditionalForm]]],
 ", and similarly for the other components.",
 " You can obtain R[1, 2, 3, 1] from R[1, 2, 1, 3] using the antisymmetry of \
the Riemann tensor under exchange of the last two indices. The antisymmetry \
under exchange of the first two indices of ",
 Cell[BoxData[
  FormBox[
   SubscriptBox["R", "\[Lambda]\[Mu]\[Nu]\[Sigma]"], TraditionalForm]]],
 " is not evident in the output because the components of ",
 Cell[BoxData[
  FormBox[
   SubscriptBox[
    SuperscriptBox["R", "\[Lambda]"], "\[Mu]\[Nu]\[Sigma]"], 
   TraditionalForm]]],
 " are displayed."
}], "Text"],

Cell[BoxData[
 RowBox[{"listriemann", ":=", 
  RowBox[{"Table", "[", 
   RowBox[{
    RowBox[{"If", "[", 
     RowBox[{
      RowBox[{"UnsameQ", "[", 
       RowBox[{
        RowBox[{"riemann", "[", 
         RowBox[{"[", 
          RowBox[{"i", ",", "j", ",", "k", ",", "l"}], "]"}], "]"}], ",", 
        "0"}], "]"}], ",", 
      RowBox[{"{", 
       RowBox[{
        RowBox[{"ToString", "[", 
         RowBox[{"R", "[", 
          RowBox[{"i", ",", "j", ",", "k", ",", "l"}], "]"}], "]"}], ",", 
        RowBox[{"riemann", "[", 
         RowBox[{"[", 
          RowBox[{"i", ",", "j", ",", "k", ",", "l"}], "]"}], "]"}]}], 
       "}"}]}], "]"}], " ", ",", 
    RowBox[{"{", 
     RowBox[{"i", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"k", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"l", ",", "1", ",", 
      RowBox[{"k", "-", "1"}]}], "}"}]}], "]"}]}]], "Input"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"TableForm", "[", 
  RowBox[{
   RowBox[{"Partition", "[", 
    RowBox[{
     RowBox[{"DeleteCases", "[", 
      RowBox[{
       RowBox[{"Flatten", "[", "listriemann", "]"}], ",", "Null"}], "]"}], 
     ",", "2"}], "]"}], ",", 
   RowBox[{"TableSpacing", "\[Rule]", 
    RowBox[{"{", 
     RowBox[{"2", ",", "2"}], "}"}]}]}], "]"}]], "Input"],

Cell[BoxData[
 TagBox[GridBox[{
    {"\<\"R[1, 2, 2, 1]\"\>", 
     FractionBox["m", "r"]},
    {"\<\"R[1, 3, 3, 1]\"\>", 
     FractionBox[
      RowBox[{"m", " ", 
       SuperscriptBox[
        RowBox[{"Sin", "[", "\[Theta]", "]"}], "2"]}], "r"]},
    {"\<\"R[1, 4, 4, 1]\"\>", 
     FractionBox[
      RowBox[{"2", " ", "m", " ", 
       RowBox[{"(", 
        RowBox[{
         RowBox[{
          RowBox[{"-", "2"}], " ", "m"}], "+", "r"}], ")"}]}], 
      SuperscriptBox["r", "4"]]},
    {"\<\"R[2, 1, 2, 1]\"\>", 
     FractionBox["m", 
      RowBox[{
       RowBox[{"(", 
        RowBox[{
         RowBox[{"2", " ", "m"}], "-", "r"}], ")"}], " ", 
       SuperscriptBox["r", "2"]}]]},
    {"\<\"R[2, 3, 3, 2]\"\>", 
     RowBox[{"-", 
      FractionBox[
       RowBox[{"2", " ", "m", " ", 
        SuperscriptBox[
         RowBox[{"Sin", "[", "\[Theta]", "]"}], "2"]}], "r"]}]},
    {"\<\"R[2, 4, 4, 2]\"\>", 
     FractionBox[
      RowBox[{"m", " ", 
       RowBox[{"(", 
        RowBox[{
         RowBox[{"2", " ", "m"}], "-", "r"}], ")"}]}], 
      SuperscriptBox["r", "4"]]},
    {"\<\"R[3, 1, 3, 1]\"\>", 
     FractionBox["m", 
      RowBox[{
       RowBox[{"(", 
        RowBox[{
         RowBox[{"2", " ", "m"}], "-", "r"}], ")"}], " ", 
       SuperscriptBox["r", "2"]}]]},
    {"\<\"R[3, 2, 3, 2]\"\>", 
     FractionBox[
      RowBox[{"2", " ", "m"}], "r"]},
    {"\<\"R[3, 4, 4, 3]\"\>", 
     FractionBox[
      RowBox[{"m", " ", 
       RowBox[{"(", 
        RowBox[{
         RowBox[{"2", " ", "m"}], "-", "r"}], ")"}]}], 
      SuperscriptBox["r", "4"]]},
    {"\<\"R[4, 1, 4, 1]\"\>", 
     RowBox[{"-", 
      FractionBox[
       RowBox[{"2", " ", "m"}], 
       RowBox[{
        RowBox[{"(", 
         RowBox[{
          RowBox[{"2", " ", "m"}], "-", "r"}], ")"}], " ", 
        SuperscriptBox["r", "2"]}]]}]},
    {"\<\"R[4, 2, 4, 2]\"\>", 
     RowBox[{"-", 
      FractionBox["m", "r"]}]},
    {"\<\"R[4, 3, 4, 3]\"\>", 
     RowBox[{"-", 
      FractionBox[
       RowBox[{"m", " ", 
        SuperscriptBox[
         RowBox[{"Sin", "[", "\[Theta]", "]"}], "2"]}], "r"]}]}
   },
   GridBoxAlignment->{
    "Columns" -> {{Left}}, "ColumnsIndexed" -> {}, "Rows" -> {{Baseline}}, 
     "RowsIndexed" -> {}},
   GridBoxSpacings->{"Columns" -> {
       Offset[0.27999999999999997`], {
        Offset[1.4]}, 
       Offset[0.27999999999999997`]}, "ColumnsIndexed" -> {}, "Rows" -> {
       Offset[0.2], {
        Offset[0.8]}, 
       Offset[0.2]}, "RowsIndexed" -> {}}],
  Function[BoxForm`e$, 
   TableForm[BoxForm`e$, TableSpacing -> {2, 2}]]]], "Output",
 CellChangeTimes->{3.646566250468894*^9}]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Calculating and displaying the Ricci tensor:", "Subsubsection"],

Cell[TextData[{
 "The Ricci tensor ",
 Cell[BoxData[
  FormBox[
   SubscriptBox["R", "\[Mu]\[Nu]"], TraditionalForm]]],
 " was defined by summing the first and third indices of the Riemann tensor \
(which has the first index already raised)."
}], "Text"],

Cell[BoxData[
 RowBox[{"ricci", ":=", 
  RowBox[{"ricci", "=", 
   RowBox[{"Simplify", "[", 
    RowBox[{"Table", "[", 
     RowBox[{
      RowBox[{"Sum", "[", 
       RowBox[{
        RowBox[{"riemann", "[", 
         RowBox[{"[", 
          RowBox[{"i", ",", "j", ",", "i", ",", "l"}], "]"}], "]"}], ",", 
        RowBox[{"{", 
         RowBox[{"i", ",", "1", ",", "n"}], "}"}]}], "]"}], ",", 
      RowBox[{"{", 
       RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
      RowBox[{"{", 
       RowBox[{"l", ",", "1", ",", "n"}], "}"}]}], "]"}], " ", 
    "]"}]}]}]], "Input"],

Cell[TextData[{
 "Next we display the nonzero components. In the output, R[1, 2] denotes ",
 Cell[BoxData[
  FormBox[
   SubscriptBox["R", "12"], TraditionalForm]]],
 ", and similarly for the other components."
}], "Text"],

Cell[BoxData[
 RowBox[{"listricci", ":=", 
  RowBox[{"Table", "[", 
   RowBox[{
    RowBox[{"If", "[", 
     RowBox[{
      RowBox[{"UnsameQ", "[", 
       RowBox[{
        RowBox[{"ricci", "[", 
         RowBox[{"[", 
          RowBox[{"j", ",", "l"}], "]"}], "]"}], ",", "0"}], "]"}], ",", 
      RowBox[{"{", 
       RowBox[{
        RowBox[{"ToString", "[", 
         RowBox[{"R", "[", 
          RowBox[{"j", ",", "l"}], "]"}], "]"}], ",", 
        RowBox[{"ricci", "[", 
         RowBox[{"[", 
          RowBox[{"j", ",", "l"}], "]"}], "]"}]}], "}"}]}], "]"}], " ", ",", 
    RowBox[{"{", 
     RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"l", ",", "1", ",", "j"}], "}"}]}], "]"}]}]], "Input"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"TableForm", "[", 
  RowBox[{
   RowBox[{"Partition", "[", 
    RowBox[{
     RowBox[{"DeleteCases", "[", 
      RowBox[{
       RowBox[{"Flatten", "[", "listricci", "]"}], ",", "Null"}], "]"}], ",", 
     "2"}], "]"}], ",", 
   RowBox[{"TableSpacing", "\[Rule]", 
    RowBox[{"{", 
     RowBox[{"2", ",", "2"}], "}"}]}]}], "]"}]], "Input"],

Cell[BoxData[
 TagBox[
  RowBox[{"{", "}"}],
  Function[BoxForm`e$, 
   TableForm[BoxForm`e$, TableSpacing -> {2, 2}]]]], "Output",
 CellChangeTimes->{3.646566250531124*^9}]
}, Open  ]],

Cell["\<\
A vanishing table (as with the Schwarzschild metric example)  means that the \
vacuum Einstein equation is satisfied.\
\>", "Text"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Calculating the scalar curvature:", "Subsubsection"],

Cell[TextData[{
 "The scalar curvature ",
 StyleBox["R",
  FontSlant->"Italic"],
 " is calculated using the inverse metric and the Ricci tensor. The result is \
displayed in the output line."
}], "Text"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"scalar", "=", 
  RowBox[{"Simplify", "[", 
   RowBox[{"Sum", "[", 
    RowBox[{
     RowBox[{
      RowBox[{"inversemetric", "[", 
       RowBox[{"[", 
        RowBox[{"i", ",", "j"}], "]"}], "]"}], 
      RowBox[{"ricci", "[", 
       RowBox[{"[", 
        RowBox[{"i", ",", "j"}], "]"}], "]"}]}], ",", 
     RowBox[{"{", 
      RowBox[{"i", ",", "1", ",", "n"}], "}"}], ",", 
     RowBox[{"{", 
      RowBox[{"j", ",", "1", ",", "n"}], "}"}]}], "]"}], " ", 
   "]"}]}]], "Input"],

Cell[BoxData["0"], "Output",
 CellChangeTimes->{3.646566250571227*^9}]
}, Open  ]]
}, Open  ]],

Cell[CellGroupData[{

Cell["Calculating the Einstein tensor:", "Subsubsection"],

Cell[TextData[{
 "The Einstein tensor,  ",
 Cell[BoxData[
  FormBox[
   RowBox[{
    SubscriptBox["G", "\[Mu]\[Nu]"], "=", 
    RowBox[{
     SubscriptBox["R", "\[Mu]\[Nu]"], "-", 
     RowBox[{
      FractionBox["1", "2"], 
      SubscriptBox["g", "\[Mu]\[Nu]"], "R"}]}]}], TraditionalForm]]],
 ", is found from the tensors already calculated."
}], "Text"],

Cell[BoxData[
 RowBox[{"einstein", ":=", 
  RowBox[{"einstein", "=", 
   RowBox[{"Simplify", "[", 
    RowBox[{"ricci", "-", 
     RowBox[{
      RowBox[{"(", 
       RowBox[{"1", "/", "2"}], ")"}], "scalar", "*", "metric"}]}], 
    "]"}]}]}]], "Input"],

Cell["\<\
The results are displayed in the same way as for the Ricci tensor earlier.\
\>", "Text"],

Cell[BoxData[
 RowBox[{"listeinstein", ":=", 
  RowBox[{"Table", "[", 
   RowBox[{
    RowBox[{"If", "[", 
     RowBox[{
      RowBox[{"UnsameQ", "[", 
       RowBox[{
        RowBox[{"einstein", "[", 
         RowBox[{"[", 
          RowBox[{"j", ",", "l"}], "]"}], "]"}], ",", "0"}], "]"}], ",", 
      RowBox[{"{", 
       RowBox[{
        RowBox[{"ToString", "[", 
         RowBox[{"G", "[", 
          RowBox[{"j", ",", "l"}], "]"}], "]"}], ",", 
        RowBox[{"einstein", "[", 
         RowBox[{"[", 
          RowBox[{"j", ",", "l"}], "]"}], "]"}]}], "}"}]}], "]"}], " ", ",", 
    RowBox[{"{", 
     RowBox[{"j", ",", "1", ",", "n"}], "}"}], ",", 
    RowBox[{"{", 
     RowBox[{"l", ",", "1", ",", "j"}], "}"}]}], "]"}]}]], "Input"],

Cell[CellGroupData[{

Cell[BoxData[
 RowBox[{"TableForm", "[", 
  RowBox[{
   RowBox[{"Partition", "[", 
    RowBox[{
     RowBox[{"DeleteCases", "[", 
      RowBox[{
       RowBox[{"Flatten", "[", "listeinstein", "]"}], ",", "Null"}], "]"}], 
     ",", "2"}], "]"}], ",", 
   RowBox[{"TableSpacing", "\[Rule]", 
    RowBox[{"{", 
     RowBox[{"2", ",", "2"}], "}"}]}]}], "]"}]], "Input"],

Cell[BoxData[
 TagBox[
  RowBox[{"{", "}"}],
  Function[BoxForm`e$, 
   TableForm[BoxForm`e$, TableSpacing -> {2, 2}]]]], "Output",
 CellChangeTimes->{3.646566250691482*^9}]
}, Open  ]],

Cell["\<\
A vanishing table means that the vacuum Einstein equation is satisfied!\
\>", "Text"]
}, Open  ]],

Cell[CellGroupData[{

Cell["Acknowledgment", "Subsubsection"],

Cell[TextData[{
 "This program was kindly written by ",
 StyleBox["Leonard Parker, University of Wisconsin, Milwaukee ",
  FontSlant->"Italic"],
 StyleBox["especially ",
  FontVariations->{"CompatibilityType"->0}],
 StyleBox[" ",
  FontSlant->"Italic"],
 StyleBox["for this text. ",
  FontVariations->{"CompatibilityType"->0}]
}], "Text"]
}, Open  ]]
}, Open  ]]
},
WindowToolbars->"EditBar",
WindowSize->{638, 699},
WindowMargins->{{0, Automatic}, {Automatic, 0}},
PrintingPageRange->{Automatic, Automatic},
PrintingOptions->{"Magnification"->1,
"PaperOrientation"->"Portrait",
"PaperSize"->{612, 792},
"PostScriptOutputFile":>FrontEnd`FileName[{$RootDirectory, "home", "hartle", 
   "131book", "math", "Curvature"}, "curvature-n.ps1", CharacterEncoding -> 
  "ISO8859-1"]},
PrivateNotebookOptions->{"VersionedStylesheet"->{"Default.nb"[8.] -> False}},
Magnification->1.5,
FrontEndVersion->"10.0 for Mac OS X x86 (32-bit, 64-bit Kernel) (June 27, \
2014)",
StyleDefinitions->"Default.nb"
]
(* End of Notebook Content *)

(* Internal cache information *)
(*CellTagsOutline
CellTagsIndex->{}
*)
(*CellTagsIndex
CellTagsIndex->{}
*)
(*NotebookFileOutline
Notebook[{
Cell[CellGroupData[{
Cell[580, 22, 55, 0, 76, "Subtitle"],
Cell[638, 24, 643, 20, 172, "Text"],
Cell[1284, 46, 674, 21, 57, "Text"],
Cell[1961, 69, 376, 14, 78, "Text"],
Cell[2340, 85, 945, 29, 50, "Text"],
Cell[3288, 116, 32, 0, 46, "Text"],
Cell[3323, 118, 220, 9, 50, "Text"],
Cell[3546, 129, 37, 0, 46, "Text"],
Cell[3586, 131, 196, 8, 50, "Text"],
Cell[3785, 141, 40, 0, 46, "Text"],
Cell[3828, 143, 290, 12, 57, "Text"],
Cell[4121, 157, 619, 15, 257, "Text"],
Cell[CellGroupData[{
Cell[4765, 176, 56, 0, 52, "Subsubsection"],
Cell[4824, 178, 205, 4, 135, "Text"],
Cell[5032, 184, 285, 6, 94, "Input"]
}, Open  ]],
Cell[CellGroupData[{
Cell[5354, 195, 47, 0, 52, "Subsubsection"],
Cell[5404, 197, 183, 6, 76, "Text"],
Cell[CellGroupData[{
Cell[5612, 207, 49, 1, 43, "Input"],
Cell[5664, 210, 70, 1, 43, "Output"]
}, Open  ]]
}, Open  ]],
Cell[CellGroupData[{
Cell[5783, 217, 56, 0, 52, "Subsubsection"],
Cell[5842, 219, 174, 3, 135, "Text"],
Cell[CellGroupData[{
Cell[6041, 226, 179, 5, 43, "Input"],
Cell[6223, 233, 147, 3, 43, "Output"]
}, Open  ]],
Cell[6385, 239, 823, 27, 225, "Text"]
}, Open  ]],
Cell[CellGroupData[{
Cell[7245, 271, 45, 0, 52, "Subsubsection"],
Cell[7293, 273, 200, 4, 135, "Text"],
Cell[CellGroupData[{
Cell[7518, 281, 906, 30, 119, "Input"],
Cell[8427, 313, 780, 26, 128, "Output"]
}, Open  ]],
Cell[9222, 342, 57, 0, 46, "Text"],
Cell[CellGroupData[{
Cell[9304, 346, 64, 1, 43, "Input"],
Cell[9371, 349, 1072, 33, 172, "Output"]
}, Open  ]]
}, Open  ]],
Cell[CellGroupData[{
Cell[10492, 388, 30, 0, 52, "Subsubsection"],
Cell[10525, 390, 1199, 40, 312, "Text"]
}, Open  ]],
Cell[CellGroupData[{
Cell[11761, 435, 56, 0, 52, "Subsubsection"],
Cell[11820, 437, 72, 0, 76, "Text"],
Cell[CellGroupData[{
Cell[11917, 441, 136, 3, 43, "Input"],
Cell[12056, 446, 747, 24, 123, "Output"]
}, Open  ]],
Cell[12818, 473, 58, 0, 46, "Text"],
Cell[CellGroupData[{
Cell[12901, 477, 71, 1, 43, "Input"],
Cell[12975, 480, 1045, 32, 182, "Output"]
}, Open  ]]
}, Open  ]],
Cell[CellGroupData[{
Cell[14069, 518, 61, 0, 52, "Subsubsection"],
Cell[14133, 520, 623, 21, 223, "Text"],
Cell[14759, 543, 1798, 50, 269, "Input"]
}, Open  ]],
Cell[CellGroupData[{
Cell[16594, 598, 60, 0, 52, "Subsubsection"],
Cell[16657, 600, 536, 12, 255, "Text"],
Cell[17196, 614, 864, 26, 144, "Input"],
Cell[CellGroupData[{
Cell[18085, 644, 364, 11, 119, "Input"],
Cell[18452, 657, 1880, 55, 380, "Output"]
}, Open  ]]
}, Open  ]],
Cell[CellGroupData[{
Cell[20381, 718, 71, 0, 88, "Subsubsection"],
Cell[20455, 720, 252, 8, 80, "Text"],
Cell[20710, 730, 1838, 51, 269, "Input"],
Cell[22551, 783, 807, 21, 292, "Text"],
Cell[23361, 806, 986, 29, 169, "Input"],
Cell[CellGroupData[{
Cell[24372, 839, 365, 11, 119, "Input"],
Cell[24740, 852, 2624, 85, 572, "Output"]
}, Open  ]]
}, Open  ]],
Cell[CellGroupData[{
Cell[27413, 943, 69, 0, 52, "Subsubsection"],
Cell[27485, 945, 254, 7, 109, "Text"],
Cell[27742, 954, 579, 17, 144, "Input"],
Cell[28324, 973, 222, 6, 106, "Text"],
Cell[28549, 981, 734, 22, 119, "Input"],
Cell[CellGroupData[{
Cell[29308, 1007, 363, 11, 119, "Input"],
Cell[29674, 1020, 173, 5, 60, "Output"]
}, Open  ]],
Cell[29862, 1028, 141, 3, 105, "Text"]
}, Open  ]],
Cell[CellGroupData[{
Cell[30040, 1036, 58, 0, 52, "Subsubsection"],
Cell[30101, 1038, 203, 6, 105, "Text"],
Cell[CellGroupData[{
Cell[30329, 1048, 505, 16, 119, "Input"],
Cell[30837, 1066, 70, 1, 43, "Output"]
}, Open  ]]
}, Open  ]],
Cell[CellGroupData[{
Cell[30956, 1073, 57, 0, 52, "Subsubsection"],
Cell[31016, 1075, 357, 12, 87, "Text"],
Cell[31376, 1089, 253, 8, 94, "Input"],
Cell[31632, 1099, 98, 2, 76, "Text"],
Cell[31733, 1103, 743, 22, 144, "Input"],
Cell[CellGroupData[{
Cell[32501, 1129, 366, 11, 119, "Input"],
Cell[32870, 1142, 173, 5, 60, "Output"]
}, Open  ]],
Cell[33058, 1150, 95, 2, 69, "Text"]
}, Open  ]],
Cell[CellGroupData[{
Cell[33190, 1157, 39, 0, 90, "Subsubsection"],
Cell[33232, 1159, 338, 10, 106, "Text"]
}, Open  ]]
}, Open  ]]
}
]
*)

(* End of internal cache information *)
